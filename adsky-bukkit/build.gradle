plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
    id 'java'
}

version '0.1.1'

sourceCompatibility = 1.8

bukkit {
    name = 'AdSky'
    main = 'fr.skyost.adsky.bukkit.AdSkyBukkitPlugin'
    description = 'Monetize your Minecraft server !'
    author = 'Skyost'
    website = 'https://github.com/Skyost/AdSky'
    apiVersion = '1.13'

    permissions {
        'adsky' {
            setDefault('OP')
            description = 'Gives access to all AdSky permissions.'
            children = ['adsky.bypass']
        }
        'adsky.bypass' {
            setDefault('OP')
            description = 'Allows to not see ads.'
        }
    }
}

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'http://repo.bstats.org/content/repositories/releases/' }
}

dependencies {
    testCompile 'junit:junit:4.12'

    compileOnly 'org.bukkit:bukkit:1.13.1-R0.1-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit-lite:1.2'

    compile project(':adsky-core')
}

shadowJar {
    version = project.version
    configurations = [project.configurations.compile]
    mergeServiceFiles()
    relocate 'org.bstats.bukkit', 'fr.skyost.adsky.bukkit.utils.bstats'
}